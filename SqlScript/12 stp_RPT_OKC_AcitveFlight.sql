GO
USE [BHSDB_OKC];
GO


ALTER PROCEDURE dbo.stp_RPT_OKC_AcitveFlight
		@DTFrom DATETIME,
		@DTTo DATETIME,
		@AIRLINES VARCHAR(MAX),
		@FLIGHTS VARCHAR(MAX)
AS
BEGIN

	CREATE TABLE #TMP_ACTIVE_FLIGHT
	(
		REPORT_DAYS VARCHAR(20),
		AIRLINE VARCHAR(3),
		FLIGHT_NUMBER VARCHAR(5),
		WEEKDAYS VARCHAR(MAX),
		STD DATETIME,
		RESOURCES VARCHAR(MAX)
	)
	
	DECLARE @TODAY DATETIME = CONVERT(datetime,CONVERT(VARCHAR,GETDATE(),103),103);
	DECLARE @YESTERDAY DATETIME = DATEADD(DAY,-1,@TODAY);
	DECLARE @TOMORROW DATETIME = DATEADD(DAY,1,@TODAY);

	--1. INSERT FLIGHTS WHOSE STD ARE BETWEEN @DTFROM AND @DTTO, AND SDO ARE TODAY
	INSERT INTO #TMP_ACTIVE_FLIGHT
	SELECT	'Today' AS REPORT_DAYS, FPS.AIRLINE, FPS.FLIGHT_NUMBER, '' AS WEEKDAYS, 
			CONVERT(datetime,CONVERT(VARCHAR,FPS.SDO,103)+' '+DBO.RPT_GETFORMATTEDSTO(FPS.STO),103) AS STD,
			'' AS RESOURCES
	FROM	FLIGHT_PLAN_SORTING FPS
	WHERE	FPS.SDO=@TODAY
		AND CONVERT(datetime,CONVERT(VARCHAR,FPS.SDO,103)+' '+DBO.RPT_GETFORMATTEDSTO(FPS.STO),103) BETWEEN @DTFROM AND @DTTO
		AND FPS.AIRLINE IN (SELECT * FROM DBO.RPT_GETPARAMETERS(@AIRLINES))
		AND FPS.FLIGHT_NUMBER IN (SELECT * FROM DBO.RPT_GETPARAMETERS(@FLIGHTS))

	--2. INSERT FLIGHTS WHICH ARE SAME WITH TODAY'S FLIGHTS, AND WHOSE SDO ARE YESTERDAY
	INSERT INTO #TMP_ACTIVE_FLIGHT
	SELECT	'Yesterday' AS REPORT_DAYS, FPS.AIRLINE, FPS.FLIGHT_NUMBER, '' AS WEEKDAYS, 
			CONVERT(datetime,CONVERT(VARCHAR,FPS.SDO,103)+' '+DBO.RPT_GETFORMATTEDSTO(FPS.STO),103) AS STD,
			'' AS RESOURCES
	FROM	FLIGHT_PLAN_SORTING FPS, #TMP_ACTIVE_FLIGHT TAF
	WHERE	FPS.SDO=@YESTERDAY
		AND TAF.REPORT_DAYS='Today'
		AND FPS.AIRLINE=TAF.AIRLINE
		AND FPS.FLIGHT_NUMBER=TAF.FLIGHT_NUMBER

	--3. INSERT FLIGHTS WHICH ARE SAME WITH TODAY'S FLIGHTS, AND WHOSE SDO ARE TOMORROW
	INSERT INTO #TMP_ACTIVE_FLIGHT
	SELECT	'Tomorrow' AS REPORT_DAYS, FPS.AIRLINE, FPS.FLIGHT_NUMBER, '' AS WEEKDAYS, 
			CONVERT(datetime,CONVERT(VARCHAR,FPS.SDO,103)+' '+DBO.RPT_GETFORMATTEDSTO(FPS.STO),103) AS STD,
			'' AS RESOURCES
	FROM	FLIGHT_PLAN_SORTING FPS, #TMP_ACTIVE_FLIGHT TAF
	WHERE	FPS.SDO=@TOMORROW
		AND TAF.REPORT_DAYS='Today'
		AND FPS.AIRLINE=TAF.AIRLINE
		AND FPS.FLIGHT_NUMBER=TAF.FLIGHT_NUMBER

	--UPDATE WEEKDAYS AND RESOURCES TO EACH FLIGHT
	DECLARE @SC_ALLOCINFO_TABLE AS ALLOCINFO_TABLETYPE; --For the parameter of stp_RPT_AllocInfoCombine
	DECLARE @SC_COMBINEINFO_TABLE AS ALLOCCOMINFO_TABLETYPE;--For the result of stp_RPT_AllocInfoCombine

	--4. UPDATE RESOURCES
	INSERT INTO @SC_ALLOCINFO_TABLE
	SELECT DISTINCT TAF.AIRLINE,TAF.FLIGHT_NUMBER,TAF.STD,FPA.RESOURCE
	FROM	#TMP_ACTIVE_FLIGHT TAF, FLIGHT_PLAN_ALLOC FPA WITH(NOLOCK)
	WHERE	TAF.AIRLINE=FPA.AIRLINE AND TAF.FLIGHT_NUMBER=FPA.FLIGHT_NUMBER 
		AND TAF.STD=CONVERT(datetime,CONVERT(VARCHAR,FPA.SDO,103)+' '+DBO.RPT_GETFORMATTEDSTO(FPA.STO),103)

	INSERT INTO @SC_COMBINEINFO_TABLE
	EXEC DBO.stp_RPT_AllocInfoCombine @SC_ALLOCINFO_TABLE

	UPDATE TAF
	SET TAF.RESOURCES=COMB.COMBINEINFO
	FROM #TMP_ACTIVE_FLIGHT TAF, @SC_COMBINEINFO_TABLE COMB
	WHERE TAF.AIRLINE=COMB.AIRLINE AND TAF.FLIGHT_NUMBER=COMB.FLIGHT_NUMBER AND TAF.STD=COMB.SDO;

	--5. UPDATE WEEKDAYS
	DELETE @SC_ALLOCINFO_TABLE;
	DELETE @SC_COMBINEINFO_TABLE;

	INSERT INTO @SC_ALLOCINFO_TABLE
	SELECT DISTINCT TAF.AIRLINE,TAF.FLIGHT_NUMBER,TAF.STD,FPA.WEEKDAY
	FROM	#TMP_ACTIVE_FLIGHT TAF, FLIGHT_PLAN_ALLOC FPA WITH(NOLOCK)
	WHERE	TAF.AIRLINE=FPA.AIRLINE AND TAF.FLIGHT_NUMBER=FPA.FLIGHT_NUMBER 
		AND TAF.STD=CONVERT(datetime,CONVERT(VARCHAR,FPA.SDO,103)+' '+DBO.RPT_GETFORMATTEDSTO(FPA.STO),103)

	INSERT INTO @SC_COMBINEINFO_TABLE
	EXEC DBO.stp_RPT_AllocInfoCombine @SC_ALLOCINFO_TABLE

	UPDATE TAF
	SET TAF.WEEKDAYS=COMB.COMBINEINFO
	FROM #TMP_ACTIVE_FLIGHT TAF, @SC_COMBINEINFO_TABLE COMB
	WHERE TAF.AIRLINE=COMB.AIRLINE AND TAF.FLIGHT_NUMBER=COMB.FLIGHT_NUMBER AND TAF.STD=COMB.SDO;

	SELECT * FROM #TMP_ACTIVE_FLIGHT;
	
END

--DECLARE @DTFrom datetime='2014-04-28 10:30:00';
--DECLARE @DTTo datetime='2014-04-29 00:00:00';
--DECLARE @AIRLINES VARCHAR(MAX)='AA,DL,UA';
--DECLARE @FLIGHTS VARCHAR(MAX)='3585,3587,3588,2560,2562';
--EXEC stp_RPT_OKC_AcitveFlight @DTFrom,@DTTo,@AIRLINES,@FLIGHTS;